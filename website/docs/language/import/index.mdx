---
page_title: Import - Configuration Language
description: >-
  Import and manage existing resources with Terraform using configuration-driven import.
---

# Import

-> **Note:** Import blocks are only available in Terraform v1.5.0 and later.

~> **Experimental:** While we do not expect to make backwards-incompatible changes to syntax, the processing of imports during the plan stage, generating configuration, and the `-generate-config-out` flag may change in future releases.

Use the `import` block to import existing infrastructure resources into Terraform, bringing them under Terraform's management. Configuration-driven import using `import` blocks is predictable, works with CICD pipelines, and allows you to preview an import operation before modifying state.

Once imported, an existing resource is present in Terraform's configuration and state, much like any other resource Terraform created. You can then manage that resource like any other, updating its attributes and destroying it as part of a standard resource lifecycle.

The `import` block records that Terraform imported a resource and that Terraform did not previously create that resource with its configuration. Put another way, the `import` block records the user's intent to add an import to Terraform's plan graph.

## Syntax

The `import` block can appear in any Terraform configuration file. A common pattern is to create an `imports.tf` file, or to place each `import` block beside the `resource` block it imports into.

```hcl
import {
  to = aws_instance.example
  id = "i-abcd1234"
}

resource "aws_instance" "example" {
  name = "hashi"
  # (other resource arguments...)
}
```

The above `import` block defines an import of the AWS instance with the ID "i-abcd1234" into the `aws_instance.example` resource in the root module.

The `import` block has the following arguments:
 - `to` - The resource instance address this import is targeting.
 - `id` - A string with the import ID of the resource.
 - `provider` (optional) - An optional custom resource provider, see [The Resource provider Meta-Argument](/terraform/language/meta-arguments/resource-provider) for details.

If you do not give the `import` block a `provider` argument, Terraform attempts to import from the default provider by default. 

## Plan and Apply an Import

Terraform processes the `import` block during the plan stage. Along with create, update, remove, and move, import is an action Terraform performs on a resource. Once a plan is approved, Terraform imports the resource into its state during the subsequent apply stage.

You can use `import` blocks by doing the following:
1. Define an `import` block for the resource(s).
1. Add a `resource` block in your configuration.
1. Run `terraform plan` to see how Terraform plans to import the resource(s).
1. Apply the configuration and have Terraform import resources and update your Terraform state.

> **Hands-on:** Try the [State Import](/terraform/tutorials/state/state-import?utm_source=WEBSITE&utm_medium=WEB_IO&utm_offer=ARTICLE_PAGE&utm_content=DOCS) tutorial.

The `import` block is [_idempotent_](https://en.wikipedia.org/wiki/Idempotence), meaning that applying an import action and running another plan will not generate another import action. 

Terraform only needs to import a resource into a single address once. Attempting to import a resource into the same address again is a (harmless) no-op. For this reason, you can remove `import` blocks after applying or safely leave them in your configuration as a record of the resource's origin for future module maintainers. For more information on maintaining configurations over time, see [Refactoring](/terraform/language/modules/develop/refactoring).

## Resource Configuration

When Terraform imports a resource, it needs you to define that resource in configuration before saving it to state. You can do this by either writing a `resource` block or by [generating configuration](/terraform/language/import/generating-configuration). 

We recommend writing a `resource` block if you know the majority of a provider's [resource arguments](/terraform/language/resources/syntax#resource-arguments). For example, if the imported resource is similar to an existing resource whose configuration you can copy and paste from.

We recommend [generating configuration](/terraform/language/import/generating-configuration) when importing multiple resources or a single complex resource that you do not already have the configuration for.

### Add a `resource` block

Add a `resource` block for the resource you are importing, ensuring the address matches the import block's target(i.e., `to`). 

```hcl
import {
  to = aws_instance.example
  id = "i-abcd1234"
}

resource "aws_instance" "example" {
  name = "renderer"
}
```

### Generate Configuration

Terraform can generate HCL for resources that do not already exist in configuration. 
For more details, see [Generating Configuration](/terraform/language/import/generating-configuration).

## Examples

The following example demonstrates how to import into a module.

```hcl
import {
  to = module.instances.aws_instance.example
  id = "i-abcd1234"
}
```

The below example shows how to import a resource that includes [`count`](/terraform/language/meta-arguments/count).

```hcl
import {
  to = aws_instance.example[0]
  id = "i-abcd1234"
}
```


The below example shows how to import a resource that includes [`for_each`](/terraform/language/meta-arguments/for_each).
```hcl
import {
  to = aws_instance.example["foo"]
  id = "i-abcd1234"
}
```

Finally, the below example demonstrates how to import from a custom resource provider.

```hcl
provider "aws" {
  alias = "europe"
  region = "eu-west-1"
}

import {
  provider = aws.europe
  to = aws_instance.example["foo"]
  id = "i-abcd1234"
}
```
