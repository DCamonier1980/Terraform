---
page_title: 'Backend Type: azurerm'
description: Terraform can store state remotely in Azure Blob Storage.
---

# azurerm

Stores the state as a Blob with the given Key within the Blob Container within [the Blob Storage Account](https://docs.microsoft.com/en-us/azure/storage/common/storage-introduction).

This backend supports state locking and consistency checking with Azure Blob Storage native capabilities.

~> **Terraform 1.1 and 1.2 supported a feature-flag to allow enabling/disabling the use of Microsoft Graph (and MSAL) rather than Azure Active Directory Graph (and ADAL) - however this flag has since been removed in Terraform 1.3. Microsoft Graph (and MSAL) are now enabled by default and Azure Active Directory Graph (and ADAL) can no longer be used.

## Authentication

The `azurerm` backend supports 3 methods of authenticating to the storage account:

- **Access Key** (default)
- **Entra ID**
- **SAS Token**

The **Access Key** method can be used directly or in combination with an Entra ID principal (e.g. user, service principal or managed identity). By default when you use an Entra ID principal, it will use the Access Token of that principal to generate an Access Key for the storage account and use that Access Key to authenticate to the state file blob. To use an Access Key directly you must generate one for your state file blob and pass it to the backend config.

The **Entra ID** method can only be used in combination with an Entra ID principal. To use the Entra ID method you must set the `use_azuread_auth` variable to `true` in your backend configuration. This will cause the backend to use the Access Token of the Entra ID principal to authenticate to the state file blob.

The **SAS Token** method can only be used directly. You must generate a SAS Token for your state file blob and pass it to the backend config.

The `azurerm` backend supports the following authentication methods to connect the storage account based on the variables that are set:

| Entra ID Authentication Method | Storage Account Authentication Type | Authentication variable * | Entra ID variable |
|-----|---|---|---|
| Entra ID User Principal via Azure CLI | Access Key | N/A | N/A |
| Entra ID User Principal via Azure CLI | Entra ID | N/A | `use_azuread_auth = true` |
| Entra ID Service Principal or User Assigned Managed Identity via OIDC (Workload identity federation) | Access Key | `use_oidc = true` | N/A |
| Entra ID Service Principal or User Assigned Managed Identity via OIDC (Workload identity federation) | Entra ID | `use_oidc = true` | `use_azuread_auth = true` |
| Entra ID Managed Identity Principal | Access Key | `use_msi = true` | N/A |
| Entra ID Managed Identity Principal | Entra ID | `use_msi = true` | `use_azuread_auth = true` |
| Entra ID Service Principal via Client Secret | Access Key | `client_secret = <service principal secret>` | N/A |
| Entra ID Service Principal via Client Secret | Entra ID | `client_secret = <service principal secret>` | `use_azuread_auth = true` |
| Entra ID Service Principal via Client Certificate | Access Key | `client_certificate_path = <cert path>` | N/A |
| Entra ID Service Principal via Client Certificate | Entra ID | `client_certificate_path = <cert path>` | `use_azuread_auth = true` |
| Access Key direct | Access Key | `access_key = <access key>` | N/A |
| SAS Key direct | SAS Token | `sas_token = <sas token>` | N/A |

-> * = We provide a single variable here, but there is usually more than one required for successful authentication. The variable we show is the one that triggers the backend to use that authentication type. You can see examples of each option below.

-> NOTE: In the table we show the variable passed as it would be hard coded. Variables can and should be passed via environment variables or partial config flags in the `init` command of the CLI.

## Example Backend Configurations

### Backend: Entra ID User via Azure CLI

This method is not suitable for automation since it only supports a User Principal. To check which tenant and subscription you are pointed to, run `az account show`.

Connect to Storage Account with Access Key (default):

```hcl
terraform {
  backend "azurerm" {
    resource_group_name  = "rg-tfstate-001"  # Can be passed via `-backend-config=`"resource_group_name=<resource group name>"` in the `init` command.
    storage_account_name = "stotfstate001"  # Can be passed via `-backend-config=`"storage_account_name=<storage account name>"` in the `init` command.
    container_name       = "prod"  # Can be passed via `-backend-config=`"container_name=<container name>"` in the `init` command.
    key                  = "terraform.tfstate"  # Can be passed via `-backend-config=`"key=<blob key name>"` in the `init` command.
  }
}
```

Connect to Storage Account with Entra ID:

```hcl
terraform {
  backend "azurerm" {
    resource_group_name  = "rg-tfstate-001"  # Can be passed via `-backend-config=`"resource_group_name=<resource group name>"` in the `init` command.
    storage_account_name = "stotfstate001"  # Can be passed via `-backend-config=`"storage_account_name=<storage account name>"` in the `init` command.
    container_name       = "prod"  # Can be passed via `-backend-config=`"container_name=<container name>"` in the `init` command.
    key                  = "terraform.tfstate"  # Can be passed via `-backend-config=`"key=<blob key name>"` in the `init` command.
    use_azuread_auth     = true  # Can also be set via `USE_AZURE_AD` environment variable.
  }
}
```

### Backend: Entra ID Service Principal or User Assigned Managed Identity via OIDC (Workload identity federation)

You can use an App Registration (Service Principal) or a User Assigned Managed Identity to configure federated credentials. You must supply the client id of the principal.

Connect to Storage Account with Access Key (default):

```hcl
terraform {
  backend "azurerm" {
    resource_group_name  = "rg-tfstate-001"  # Can be passed via `-backend-config=`"resource_group_name=<resource group name>"` in the `init` command.
    storage_account_name = "stotfstate001"  # Can be passed via `-backend-config=`"storage_account_name=<storage account name>"` in the `init` command.
    container_name       = "prod"  # Can be passed via `-backend-config=`"container_name=<container name>"` in the `init` command.
    key                  = "terraform.tfstate"  # Can be passed via `-backend-config=`"key=<blob key name>"` in the `init` command.
    use_oidc              = true  # Can also be set via `USE_OIDC` environment variable.
    client_id            = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_CLIENT_ID` environment variable.
    subscription_id      = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_SUBSCRIPTION_ID` environment variable.
    tenant_id            = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_TENANT_ID` environment variable.
  }
}
```

Connect to Storage Account with Entra ID:

```hcl
terraform {
  backend "azurerm" {
    resource_group_name  = "rg-tfstate-001"  # Can be passed via `-backend-config=`"resource_group_name=<resource group name>"` in the `init` command.
    storage_account_name = "stotfstate001"  # Can be passed via `-backend-config=`"storage_account_name=<storage account name>"` in the `init` command.
    container_name       = "prod"  # Can be passed via `-backend-config=`"container_name=<container name>"` in the `init` command.
    key                  = "terraform.tfstate"  # Can be passed via `-backend-config=`"key=<blob key name>"` in the `init` command.
    use_oidc              = true  # Can also be set via `USE_OIDC` environment variable.
    client_id            = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_CLIENT_ID` environment variable.
    subscription_id      = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_SUBSCRIPTION_ID` environment variable.
    tenant_id            = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_TENANT_ID` environment variable.
    use_azuread_auth     = true  # Can also be set via `USE_AZURE_AD` environment variable.
  }
}
```

### Backend: Entra ID Managed Identity Principal

You can use a User Assigned Managed Identity as well as a System Assigned Managed Identity on your agent / runner compute. However the authentication does not support specifiying the Client ID of the User Assigned Managed Identity, so you can only supply one per compute instance.

Connect to Storage Account with Access Key (default):

```hcl
terraform {
  backend "azurerm" {
    resource_group_name  = "rg-tfstate-001"  # Can be passed via `-backend-config=`"resource_group_name=<resource group name>"` in the `init` command.
    storage_account_name = "stotfstate001"  # Can be passed via `-backend-config=`"storage_account_name=<storage account name>"` in the `init` command.
    container_name       = "prod"  # Can be passed via `-backend-config=`"container_name=<container name>"` in the `init` command.
    key                  = "terraform.tfstate"  # Can be passed via `-backend-config=`"key=<blob key name>"` in the `init` command.
    use_msi              = true  # Can also be set via `USE_MSI` environment variable.
    subscription_id      = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_SUBSCRIPTION_ID` environment variable.
    tenant_id            = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_TENANT_ID` environment variable.
  }
}
```

Connect to Storage Account with Entra ID:

```hcl
terraform {
  backend "azurerm" {
    resource_group_name  = "rg-tfstate-001"  # Can be passed via `-backend-config=`"resource_group_name=<resource group name>"` in the `init` command.
    storage_account_name = "stotfstate001"  # Can be passed via `-backend-config=`"storage_account_name=<storage account name>"` in the `init` command.
    container_name       = "prod"  # Can be passed via `-backend-config=`"container_name=<container name>"` in the `init` command.
    key                  = "terraform.tfstate"  # Can be passed via `-backend-config=`"key=<blob key name>"` in the `init` command.
    use_msi              = true  # Can also be set via `USE_MSI` environment variable.
    subscription_id      = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_SUBSCRIPTION_ID` environment variable.
    tenant_id            = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_TENANT_ID` environment variable.
    use_azuread_auth     = true  # Can also be set via `USE_AZURE_AD` environment variable.
  }
}
```

### Backend: Entra ID Service Principal via Client Secret

~> **Warning!**: This method requires you to manage and rotate a secret. Consider using OIDC as a more secure approach.

Connect to Storage Account with Access Key (default):

```hcl
terraform {
  backend "azurerm" {
    resource_group_name  = "rg-tfstate-001"  # Can be passed via `-backend-config=`"resource_group_name=<resource group name>"` in the `init` command.
    storage_account_name = "stotfstate001"  # Can be passed via `-backend-config=`"storage_account_name=<storage account name>"` in the `init` command.
    container_name       = "prod"  # Can be passed via `-backend-config=`"container_name=<container name>"` in the `init` command.
    key                  = "terraform.tfstate"  # Can be passed via `-backend-config=`"key=<blob key name>"` in the `init` command.
    client_id            = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_CLIENT_ID` environment variable.
    client_secret        = "************************************"  # Can also be set via `ARM_CLIENT_SECRET` environment variable.
    subscription_id      = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_SUBSCRIPTION_ID` environment variable.
    tenant_id            = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_TENANT_ID` environment variable.
  }
}
```

Connect to Storage Account with Entra ID:

```hcl
terraform {
  backend "azurerm" {
    resource_group_name  = "rg-tfstate-001"  # Can be passed via `-backend-config=`"resource_group_name=<resource group name>"` in the `init` command.
    storage_account_name = "stotfstate001"  # Can be passed via `-backend-config=`"storage_account_name=<storage account name>"` in the `init` command.
    container_name       = "prod"  # Can be passed via `-backend-config=`"container_name=<container name>"` in the `init` command.
    key                  = "terraform.tfstate"  # Can be passed via `-backend-config=`"key=<blob key name>"` in the `init` command.
    client_id            = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_CLIENT_ID` environment variable.
    client_secret        = "************************************"  # Can also be set via `ARM_CLIENT_SECRET` environment variable.
    subscription_id      = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_SUBSCRIPTION_ID` environment variable.
    tenant_id            = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_TENANT_ID` environment variable.
    use_azuread_auth     = true  # Can also be set via `USE_AZURE_AD` environment variable.
  }
}
```

### Backend: Entra ID Service Principal via Client Certificate

~> **Warning!**: This method requires you to manage and rotate a secret. Consider using OIDC as a more secure approach.

Connect to Storage Account with Access Key (default):

```hcl
terraform {
  backend "azurerm" {
    resource_group_name         = "rg-tfstate-001"  # Can be passed via `-backend-config=`"resource_group_name=<resource group name>"` in the `init` command.
    storage_account_name        = "stotfstate001"  # Can be passed via `-backend-config=`"storage_account_name=<storage account name>"` in the `init` command.
    container_name              = "prod"  # Can be passed via `-backend-config=`"container_name=<container name>"` in the `init` command.
    key                         = "terraform.tfstate"  # Can be passed via `-backend-config=`"key=<blob key name>"` in the `init` command.
    client_id                   = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_CLIENT_ID` environment variable.
    client_certificate_path     = "./cert.pfx"  # Can also be set via `ARM_CLIENT_CERTIFICATE_PATH` environment variable.
    client_certificate_password = "************************************"  # Can also be set via `ARM_CLIENT_CERTIFICATE_PASSWORD` environment variable.
    subscription_id             = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_SUBSCRIPTION_ID` environment variable.
    tenant_id                   = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_TENANT_ID` environment variable.
  }
}
```

Connect to Storage Account with Entra ID:

```hcl
terraform {
  backend "azurerm" {
    resource_group_name         = "rg-tfstate-001"  # Can be passed via `-backend-config=`"resource_group_name=<resource group name>"` in the `init` command.
    storage_account_name        = "stotfstate001"  # Can be passed via `-backend-config=`"storage_account_name=<storage account name>"` in the `init` command.
    container_name              = "prod"  # Can be passed via `-backend-config=`"container_name=<container name>"` in the `init` command.
    key                         = "terraform.tfstate"  # Can be passed via `-backend-config=`"key=<blob key name>"` in the `init` command.
    client_id                   = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_CLIENT_ID` environment variable.
    client_certificate_path     = "./cert.pfx"  # Can also be set via `ARM_CLIENT_CERTIFICATE_PATH` environment variable.
    client_certificate_password = "************************************"  # Can also be set via `ARM_CLIENT_CERTIFICATE_PASSWORD` environment variable.
    subscription_id             = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_SUBSCRIPTION_ID` environment variable.
    tenant_id                   = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_TENANT_ID` environment variable.
    use_azuread_auth            = true  # Can also be set via `USE_AZURE_AD` environment variable.
  }
}
```

### Backend: Access Key Direct

~> **Warning!**: This method requires you to manage and rotate a secret. Consider using OIDC as a more secure approach.

```hcl
terraform {
  backend "azurerm" {
    resource_group_name  = "rg-tfstate-001"  # Can be passed via `-backend-config=`"resource_group_name=<resource group name>"` in the `init` command.
    storage_account_name = "stotfstate001"  # Can be passed via `-backend-config=`"storage_account_name=<storage account name>"` in the `init` command.
    container_name       = "prod"  # Can be passed via `-backend-config=`"container_name=<container name>"` in the `init` command.
    key                  = "terraform.tfstate"  # Can be passed via `-backend-config=`"key=<blob key name>"` in the `init` command.
    access_key           = "abcdefghijklmnopqrstuvwxyz0123456789..."  # Can also be set via `ARM_ACCESS_KEY` environment variable.
  }
}
```

### Backend: SAS Token

~> **Warning!**: This method requires you to manage and a rotate a secret. Consider using OIDC as a more secure approach.

```hcl
terraform {
  backend "azurerm" {
    resource_group_name  = "rg-tfstate-001"  # Can be passed via `-backend-config=`"resource_group_name=<resource group name>"` in the `init` command.
    storage_account_name = "stotfstate001"  # Can be passed via `-backend-config=`"storage_account_name=<storage account name>"` in the `init` command.
    container_name       = "prod"  # Can be passed via `-backend-config=`"container_name=<container name>"` in the `init` command.
    key                  = "terraform.tfstate"  # Can be passed via `-backend-config=`"key=<blob key name>"` in the `init` command.
    sas_token            = "abcdefghijklmnopqrstuvwxyz0123456789..."  # Can also be set via `ARM_SAS_TOKEN` environment variable.
  }
}
```

## Example Data Source Configurations

### Data Source: Entra ID User Principal via Azure CLI

This method is not suitable for automation since it only supports a User Principal. To check which tenant and subscription you are pointed to, run `az account show`.

Connect to Storage Account with Access Key (default):

```hcl
data "terraform_remote_state" "foo" {
  backend = "azurerm"
  config = {
    resource_group_name  = "rg-tfstate-001"
    storage_account_name = "stotfstate001"
    container_name       = "prod"
    key                  = "terraform.tfstate"
  }
}
```

Connect to Storage Account with Entra ID:

```hcl
data "terraform_remote_state" "foo" {
  backend = "azurerm"
  config = {
    resource_group_name  = "rg-tfstate-001"
    storage_account_name = "stotfstate001"
    container_name       = "prod"
    key                  = "terraform.tfstate"
    use_azuread_auth     = true  # Can also be set via `USE_AZURE_AD` environment variable.
  }
}
```

### Data Source: Entra ID Service Principal or User Assigned Managed Identity via OIDC (Workload identity federation)

You can use an App Registration (Service Principal) or a User Assigned Managed Identity to configure federated credentials. You must supply the client id of the principal.

Connect to Storage Account with Access Key (default):

```hcl
data "terraform_remote_state" "foo" {
  backend = "azurerm"
  config = {
    resource_group_name  = "rg-tfstate-001"
    storage_account_name = "stotfstate001"
    container_name       = "prod"
    key                  = "terraform.tfstate"
    use_oidc             = true  # Can also be set via `USE_OIDC` environment variable.
    client_id            = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_CLIENT_ID` environment variable.
    subscription_id      = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_SUBSCRIPTION_ID` environment variable.
    tenant_id            = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_TENANT_ID` environment variable.
  }
}
```

Connect to Storage Account with Entra ID:

```hcl
data "terraform_remote_state" "foo" {
  backend = "azurerm"
  config = {
    resource_group_name  = "rg-tfstate-001"
    storage_account_name = "stotfstate001"
    container_name       = "prod"
    key                  = "terraform.tfstate"
    use_oidc             = true  # Can also be set via `USE_OIDC` environment variable.
    client_id            = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_CLIENT_ID` environment variable.
    subscription_id      = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_SUBSCRIPTION_ID` environment variable.
    tenant_id            = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_TENANT_ID` environment variable.
    use_azuread_auth     = true  # Can also be set via `USE_AZURE_AD` environment variable.
  }
}
```

### Data Source: Entra ID Managed Identity Principal

You can use a User Assigned Managed Identity as well as a System Assigned Managed Identity on your agent / runner compute. However the authentication does not support specifiying the Client ID of the User Assigned Managed Identity, so you can only supply one per compute instance.

Connect to Storage Account with Access Key (default):

```hcl
data "terraform_remote_state" "foo" {
  backend = "azurerm"
  config = {
    resource_group_name  = "rg-tfstate-001"
    storage_account_name = "stotfstate001"
    container_name       = "prod"
    key                  = "terraform.tfstate"
    use_msi              = true  # Can also be set via `USE_MSI` environment variable.
    subscription_id      = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_SUBSCRIPTION_ID` environment variable.
    tenant_id            = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_TENANT_ID` environment variable.
  }
}
```

Connect to Storage Account with Entra ID:

```hcl
data "terraform_remote_state" "foo" {
  backend = "azurerm"
  config = {
    resource_group_name  = "rg-tfstate-001"
    storage_account_name = "stotfstate001"
    container_name       = "prod"
    key                  = "terraform.tfstate"
    use_msi              = true  # Can also be set via `USE_MSI` environment variable.
    subscription_id      = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_SUBSCRIPTION_ID` environment variable.
    tenant_id            = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_TENANT_ID` environment variable.
    use_azuread_auth     = true  # Can also be set via `USE_AZURE_AD` environment variable.
  }
}
```

### Data Source: Entra ID Service Principal via Client Secret

~> **Warning!**: This method requires you to manage and a rotate a secret. Consider using OIDC as a more secure approach.

Connect to Storage Account with Access Key (default):

```hcl
data "terraform_remote_state" "foo" {
  backend = "azurerm"
  config = {
    resource_group_name  = "rg-tfstate-001"
    storage_account_name = "stotfstate001"
    container_name       = "prod"
    key                  = "terraform.tfstate"
    client_id            = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_CLIENT_ID` environment variable.
    client_secret        = "************************************"  # Can also be set via `ARM_CLIENT_SECRET` environment variable.
    subscription_id      = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_SUBSCRIPTION_ID` environment variable.
    tenant_id            = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_TENANT_ID` environment variable.
  }
}
```

Connect to Storage Account with Entra ID:

```hcl
data "terraform_remote_state" "foo" {
  backend = "azurerm"
  config = {
    resource_group_name  = "rg-tfstate-001"
    storage_account_name = "stotfstate001"
    container_name       = "prod"
    key                  = "terraform.tfstate"
    client_id            = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_CLIENT_ID` environment variable.
    client_secret        = "************************************"  # Can also be set via `ARM_CLIENT_SECRET` environment variable.
    subscription_id      = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_SUBSCRIPTION_ID` environment variable.
    tenant_id            = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_TENANT_ID` environment variable.
    use_azuread_auth     = true  # Can also be set via `USE_AZURE_AD` environment variable.
  }
}
```

### Data Source: Entra ID Service Principal via Client Certificate

~> **Warning!**: This method requires you to manage and a rotate a secret. Consider using OIDC as a more secure approach.

Connect to Storage Account with Access Key (default):

```hcl
data "terraform_remote_state" "foo" {
  backend = "azurerm"
  config = {
    resource_group_name  = "rg-tfstate-001"
    storage_account_name = "stotfstate001"
    container_name       = "prod"
    key                  = "terraform.tfstate"
    client_id                   = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_CLIENT_ID` environment variable.
    client_certificate_path     = "./cert.pfx"  # Can also be set via `ARM_CLIENT_CERTIFICATE_PATH` environment variable.
    client_certificate_password = "************************************"  # Can also be set via `ARM_CLIENT_CERTIFICATE_PASSWORD` environment variable.
    subscription_id             = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_SUBSCRIPTION_ID` environment variable.
    tenant_id                   = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_TENANT_ID` environment variable.
  }
}
```

Connect to Storage Account with Entra ID:

```hcl
data "terraform_remote_state" "foo" {
  backend = "azurerm"
  config = {
    resource_group_name  = "rg-tfstate-001"
    storage_account_name = "stotfstate001"
    container_name       = "prod"
    key                  = "terraform.tfstate"
    client_id                   = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_CLIENT_ID` environment variable.
    client_certificate_path     = "./cert.pfx"  # Can also be set via `ARM_CLIENT_CERTIFICATE_PATH` environment variable.
    client_certificate_password = "************************************"  # Can also be set via `ARM_CLIENT_CERTIFICATE_PASSWORD` environment variable.
    subscription_id             = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_SUBSCRIPTION_ID` environment variable.
    tenant_id                   = "00000000-0000-0000-0000-000000000000"  # Can also be set via `ARM_TENANT_ID` environment variable.
    use_azuread_auth            = true  # Can also be set via `USE_AZURE_AD` environment variable.
  }
}
```

### Data Source: Access Key Direct

~> **Warning!**: This method requires you to manage and a rotate a secret. Consider using OIDC as a more secure approach.

```hcl
data "terraform_remote_state" "foo" {
  backend = "azurerm"
  config = {
    resource_group_name  = "rg-tfstate-001"
    storage_account_name = "stotfstate001"
    container_name       = "prod"
    key                  = "terraform.tfstate"
    access_key           = "abcdefghijklmnopqrstuvwxyz0123456789..."  # Can also be set via `ARM_ACCESS_KEY` environment variable.
  }
}
```

### Data Source: SAS Token

~> **Warning!**: This method requires you to manage and a rotate a secret. Consider using OIDC as a more secure approach.

```hcl
data "terraform_remote_state" "foo" {
  backend = "azurerm"
  config = {
    resource_group_name  = "rg-tfstate-001"
    storage_account_name = "stotfstate001"
    container_name       = "prod"
    key                  = "terraform.tfstate"
    sas_token            = "abcdefghijklmnopqrstuvwxyz0123456789..."  # Can also be set via `ARM_SAS_TOKEN` environment variable.
  }
}
```

## Configuration Variables

!> ****Warning!**:**  We recommend using environment variables to supply credentials and other sensitive data. If you use `-backend-config` or hardcode these values directly in your configuration, Terraform will include these values in both the `.terraform` subdirectory and in plan files. Refer to [Credentials and Sensitive Data](/terraform/language/settings/backends/configuration#credentials-and-sensitive-data) for details.

The following configuration options are supported:

* `storage_account_name` - (Required) The Name of [the Storage Account](https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/storage_account).

* `container_name` - (Required) The Name of [the Storage Container](https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/storage_container) within the Storage Account.

* `key` - (Required) The name of the Blob used to retrieve/store Terraform's State file inside the Storage Container.

* `environment` - (Optional) The Azure Environment which should be used. This can also be sourced from the `ARM_ENVIRONMENT` environment variable. Possible values are `public`, `china`, `german`, `stack` and `usgovernment`. Defaults to `public`.

* `endpoint` - (Optional) The Custom Endpoint for Azure Resource Manager. This can also be sourced from the `ARM_ENDPOINT` environment variable.

~> **NOTE:** An `endpoint` should only be configured when using Azure Stack.

* `metadata_host` - (Optional) The Hostname of the Azure Metadata Service (for example `management.azure.com`), used to obtain the Cloud Environment when using a Custom Azure Environment. This can also be sourced from the `ARM_METADATA_HOSTNAME` Environment Variable.

* `snapshot` - (Optional) Should the Blob used to store the Terraform Statefile be snapshotted before use? Defaults to `false`. This value can also be sourced from the `ARM_SNAPSHOT` environment variable.

***

When authenticating using a Managed Identity (MSI) - the following fields are also supported:

* `resource_group_name` - (Required) The Name of the Resource Group in which the Storage Account exists.

* `msi_endpoint` - (Optional) The path to a custom Managed Service Identity endpoint which is automatically determined if not specified. This can also be sourced from the `ARM_MSI_ENDPOINT` environment variable.

* `subscription_id` - (Optional) The Subscription ID in which the Storage Account exists. This can also be sourced from the `ARM_SUBSCRIPTION_ID` environment variable.

* `tenant_id` - (Optional) The Tenant ID in which the Subscription exists. This can also be sourced from the `ARM_TENANT_ID` environment variable.

* `use_msi` - (Optional) Should Managed Service Identity authentication be used? This can also be sourced from the `ARM_USE_MSI` environment variable.

***

When authenticating using a Service Principal with OpenID Connect (OIDC / Workload identity federation)  - the following fields are also supported:

* `oidc_request_url` - (Optional) The URL for the OIDC provider from which to request an ID token. This can also be sourced from the `ARM_OIDC_REQUEST_URL` or `ACTIONS_ID_TOKEN_REQUEST_URL` environment variables.

* `oidc_request_token` - (Optional) The bearer token for the request to the OIDC provider. This can also be sourced from the `ARM_OIDC_REQUEST_TOKEN` or `ACTIONS_ID_TOKEN_REQUEST_TOKEN` environment variables.

* `oidc_token` - (Optional) The ID token when authenticating using OpenID Connect (OIDC). This can also be sourced from the `ARM_OIDC_TOKEN` environment variable.

* `oidc_token_file_path` - (Optional) The path to a file containing an ID token when authenticating using OpenID Connect (OIDC). This can also be sourced from the `ARM_OIDC_TOKEN_FILE_PATH` environment variable.

* `use_oidc` - (Optional) Should OIDC authentication be used? This can also be sourced from the `ARM_USE_OIDC` environment variable.

***

When authenticating using a SAS Token associated with the Storage Account - the following fields are also supported:

* `sas_token` - (Optional) The SAS Token used to access the Blob Storage Account. This can also be sourced from the `ARM_SAS_TOKEN` environment variable.

***

When authenticating using the Storage Account's Access Key - the following fields are also supported:

* `access_key` - (Optional) The Access Key used to access the Blob Storage Account. This can also be sourced from the `ARM_ACCESS_KEY` environment variable.

***

When authenticating using Entra ID service principal, you have the option to use Entra ID authentication for the storage account (as opposed to Access Key or SAS Token) - the following fields are also supported:

* `use_azuread_auth` - (Optional) Should EntraID Authentication be used to access the Blob Storage Account. This can also be sourced from the `ARM_USE_AZUREAD` environment variable.

-> **Note:** When using Entra ID for Authentication to Storage you need to ensure the `Storage Blob Data Owner` or `Container Blob Data Owner` roles are assigned.

***

When authenticating using a Service Principal with a Client Certificate - the following fields are also supported:

* `resource_group_name` - (Required) The Name of the Resource Group in which the Storage Account exists.

* `client_id` - (Optional) The Client ID of the Service Principal. This can also be sourced from the `ARM_CLIENT_ID` environment variable.

* `client_certificate_password` - (Optional) The password associated with the Client Certificate specified in `client_certificate_path`. This can also be sourced from the `ARM_CLIENT_CERTIFICATE_PASSWORD` environment variable.

* `client_certificate_path` - (Optional) The path to the PFX file used as the Client Certificate when authenticating as a Service Principal. This can also be sourced from the `ARM_CLIENT_CERTIFICATE_PATH` environment variable.

* `subscription_id` - (Optional) The Subscription ID in which the Storage Account exists. This can also be sourced from the `ARM_SUBSCRIPTION_ID` environment variable.

* `tenant_id` - (Optional) The Tenant ID in which the Subscription exists. This can also be sourced from the `ARM_TENANT_ID` environment variable.

***

When authenticating using a Service Principal with a Client Secret - the following fields are also supported:

* `resource_group_name` - (Required) The Name of the Resource Group in which the Storage Account exists.

* `client_id` - (Optional) The Client ID of the Service Principal. This can also be sourced from the `ARM_CLIENT_ID` environment variable.

* `client_secret` - (Optional) The Client Secret of the Service Principal. This can also be sourced from the `ARM_CLIENT_SECRET` environment variable.

* `subscription_id` - (Optional) The Subscription ID in which the Storage Account exists. This can also be sourced from the `ARM_SUBSCRIPTION_ID` environment variable.

* `tenant_id` - (Optional) The Tenant ID in which the Subscription exists. This can also be sourced from the `ARM_TENANT_ID` environment variable.
