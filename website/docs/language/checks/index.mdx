---
page_title: Checks - Configuration Language
description: >-
  Check customized infrastructure requirements to provide ongoing and continuous verification.
---

# Checks

-> **Note:** Check blocks are only available in Terraform v1.5.0 and later.

The `check` block can validate your infrastructure outside the usual resource lifecycle. Check blocks address a gap between post-apply and functional validation of infrastructure. Check blocks allow you to define [custom conditions](/terraform/language/expressions/custom-conditions) that execute on every Terraform plan or apply operation without affecting the overall status of an operation. Check blocks execute as the last step of a plan or apply, after Terraform has planned or provisioned your infrastructure.

## Checks Syntax

You can declare a `check` block with a local name, zero-to-one scoped [data sources](#scoped-data-sources), and one-to-many [assertions](#assertions).

The following example loads the Terraform website and validates that it returns the expected status code of `200`.

```hcl
check "health_check" {
  data "http" "terraform_io" {
    url = "https://www.terraform.io"
  }

  assert {
    condition = data.http.terraform_io.status_code == 200
    error_message = "${data.http.terraform_io.url} returned an unhealthy status code"
  }
}
```

### Scoped Data Sources

A `check` block can optionally contain a nested (a.k.a scoped) data source. This `data` block behaves like an external [data source](/terraform/language/data-sources), except you can not reference it outside its enclosing `check` block. Additionally, if a scoped data source's provider raises any errors, they are masked as warnings and do not prevent Terraform from continuing operation execution.

You can use a scoped data source to validate the status of a piece of infrastructure outside of the usual Terraform resource lifecycle. [In the above example](#checks-syntax), if the `terraform_io` data source fails to load, you receive a warning instead of a blocking error, which would occur if you declared this data source outside of a `check` block.

Any data source from any provider can be used within a check block.

#### Meta-Arguments

Scoped data sources support the `depends_on` and `provider` [meta-arguments](/terraform/language/resources/syntax#meta-arguments). Scoped data sources do not support the `count`, `foreach`, or `lifecycle` meta-arguments.

### Assertions

Check blocks validate your custom assertions using `assert` blocks. Each `check` block must have at least one, but potentially many, `assert` blocks. Each `assert` block has a [`condition` attribute](/terraform/language/expressions/custom-conditions#condition-expressions) and an [`error_message` attribute](/terraform/language/expressions/custom-conditions#error-messages).

Unlike other [custom conditions](/terraform/language/expressions/custom-conditions), assertions do not affect Terraform's execution of an operation. A failed assertion reports a warning without halting the ongoing operation. This contrasts with other custom conditions, such as a postcondition, where Terraform produces an error immediately, halting the operation and blocking the application or planning of future resources.

Condition arguments within `assert` blocks can refer to scoped data sources within the enclosing `check` block and any variables, resources, data sources, or module outputs within the current module.

[Learn more about assertions](/terraform/language/expressions/custom-conditions#check-assertions).

### Meta-Arguments

In the initial release of checks, none of the traditional resource [meta-arguments](/terraform/language/resources/syntax#meta-arguments) are supported. We are interested to see how the community uses this new feature, so please file feedback with use-cases that would benefit from the introduction of any meta-arguments.

## Continuous Validation in Terraform Cloud

Terraform Cloud can automatically validate whether checks in a workspaceâ€™s configuration continue to pass after Terraform provisions new infrastructure. See [Continuous Validation](/terraform/cloud-docs/workspaces/health#continuous-validation) for details.

## Choosing Checks or other Custom Conditions

In many cases, resource or data source postconditions and check blocks can be used interchangeably to model similar validations. For example, compare the following HCL, which details the creation of a health check directly as a data source postcondition, to the check block implementation provided in the [Checks Syntax](#checks-syntax) section.

```hcl
data "http" "terraform_io" {
  url = "https://www.terraform.io"

  lifecycle {
    postcondition {
        condition = self.status_code == 200
        error_message = "${self.url} returned an unhealthy status code"
    }
  }
}
```

Both these implementations will validate that the status code is returned as expected during a plan or an apply operation. However, they differ in how they respond when the validation fails. The data source postcondition will fail and block the continued execution of any Terraform operation while the check block is permissive and will not interfere with the overall Terraform operation.

This means that recovering from a failure is impossible with a postcondition as Terraform will block any plan or apply operations as long as the postcondition is unsatisfied during the plan. This problem stems from the postcondition not being directly dependent on the Terraform configuration but instead on the complex interactions between multiple resources.

With this contrast in mind, check blocks should therefore be used to validate the status of the infrastructure as a whole compared to a postcondition which should provide a guarantee on a single resource based on that resource's configuration.
